#ident	"@(#)lp.admin:printers/printers/add/Form.access	1.2"

form="Setup Printer Access"

help=OPEN TEXT $INTFBASE/Text.itemhelp $LININFO

`indicator -w;set -e -l name_2=/tmp/users$VPID -l name_3=/tmp/forms$VPID;`

done=`
	set -e toclose="$toclose $OAMBASE/add-ons/lp/printers/printers/add/Form.access";
	message -w "Saving printer access information, please wait.";

	if [ "$F2" = "none" ];
	then
		lpadmin -p $lp_printer -udeny:all > /dev/null 2> $error;
	else
		lpadmin -p $lp_printer -uallow:$F2 > /dev/null 2> $error;
	fi;

	if [ "$F3" = "none" ];
	then
		lpadmin -p $lp_printer -fdeny:all > /dev/null 2> $error;
	else
		lpadmin -p $lp_printer -fallow:$F3 > /dev/null 2> $error;
	fi;

	if [ "$RET" != "0" -a -s $error ];
	then
		echo "OPEN TEXT $OAMBASE/add-ons/lp/printers/printers/add/Text.access";
	else
		rm -f $name_2 $name_3;
		message -w "The printer access information has been saved.";
		sleep 3;
		if [ "$use_stdprt" = "no" -a "$lp_fldsys" = "$lp_system" ];
		then
			message -w "Going to next subtask, please wait.";
			echo "OPEN FORM $OAMBASE/add-ons/lp/printers/printers/add/Form.comm.lcl";
		else
	    		message -w "Printer add complete.";
			rm -f $datafile $error;
	    		echo "close $toclose";
		fi;
	fi;
`


#F1
name=Printer:
nrow=1
ncol=1
frow=1
fcol=10
rows=1
columns=14
value=$lp_printer
inactive=TRUE

#F2
name="Users who are allowed access to this printer:"
nrow=3
ncol=1
frow=3
fcol=47
rows=1
columns=25
scroll=true
value=all
rmenu=OPEN MENU $OAMBASE/add-ons/lp/printers/printers/add/Menu.ch_users
valid=`indicator -w;
	unset -l bad_one;
	/usr/bin/awk -F: '$3 >= 100 {print $1 }' /etc/passwd > $name_2;
	echo "all" >> $name_2;
	echo "none" >> $name_2;
	if [ -z "$F2" ];
	then
		set -l no_value=true -l name_valid=false;
	else
		set -l no_value=false -l name_valid=true;
	fi;
	echo "$F2" | tr -s "," " " |  tr -s " " "\012" | regex -e '^(.*)$0$'
	'`fmlgrep "^$m0$" $name_2 || set -l bad_one="$m0" -l name_valid=false`' > /dev/null`$name_valid

invalidmsg=`if [ "$no_value" = "true" ];
	then
		echo "You must enter a value for this field. Strike CHOICES for valid users.";
	else
		echo "$bad_one is not a valid user. Strike CHOICES for valid users.";
	fi`
fieldmsg="Press [CHOICES] for users.  Press [SAVE] when form is completed."

#F3
name="Forms allowed on this printer:"
nrow=5
ncol=1
frow=5
fcol=32
rows=1
columns=31
scroll=true
value=none
rmenu=OPEN MENU $OAMBASE/add-ons/lp/printers/printers/add/Menu.ch_forms
valid=`indicator -w;
	unset -l bad_one;
	ls /etc/lp/forms > $name_3;
	echo "all" >> $name_3;
	echo "none" >> $name_3;
	if [ -z "$F3" ];
	then
		set -l no_value=true -l name_valid=false;
	else
		set -l no_value=false -l name_valid=true;
	fi;
	echo "$F3" | tr -s "," " " |  tr -s " " "\012" | regex -e '^(.*)$0$'
	'`fmlgrep "^$m0$" $name_3 || set -l bad_one="$m0" -l name_valid=false`' > /dev/null`$name_valid

invalidmsg=`if [ "$no_value" = "true" ];
	then
		echo "You must enter a value for this field. Strike CHOICES for valid forms.";
	else
		echo "$bad_one is not a valid form. Strike CHOICES for valid forms.";
	fi`
fieldmsg="Press [CHOICES] for valid forms.  Press [SAVE] when form is completed."
