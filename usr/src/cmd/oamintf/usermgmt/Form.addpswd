#ident	"@(#)oamintf:usermgmt/Form.addpswd	1.2.1.6"

################################################################################
#	Module Name:  Form.addpswd
################################################################################
form="Define User Password Information"

framemsg=`readfile $INTFBASE/form.msg`

`set -l LIFE=longterm`
lifetime=$LIFE

help=OPEN TEXT $INTFBASE/Text.itemhelp $LININFO

close=`rm -f /tmp/gadderr`

done=`message -w "Defining Password Information";

	if [ "$F1" = "lock" -o "$F1" = "no password" ];
	then
		pswdchk $ARG1 $F1 $F2 $F3 $F4;

		if [ $RET -eq 0 ];
		then
			set -l LIFE=shortterm;
			set -l CMD="OPEN TEXT $OBJ_DIR/Text.apswdok $ARG1";
		else
	    		set -l CMD="OPEN TEXT $OBJ_DIR/Text.apswdnok";
		fi;

	elif [ "$F1" = "password" ];
	then
		run -n pswddef $ARG1;

		if [ $RET -eq 0 ];
		then
			pswdchk $ARG1 $F1 $F2 $F3 $F4;

			if [ $RET -eq 0 ];
			then
				set -l LIFE=shortterm;
				set -l CMD="OPEN TEXT $OBJ_DIR/Text.apswdok $ARG1";
			else
	    			set -l CMD="OPEN TEXT $OBJ_DIR/Text.apswdnok";
			fi;
		else
	    		set -l CMD="OPEN TEXT $OBJ_DIR/Text.apswdnok";
		fi;

	fi`$CMD

name=Password status:
lininfo=Form.addpswd:pwstat
nrow=1
ncol=3
frow=1
fcol=20
rows=1
columns=12

rmenu={ "lock" "no password" "password" }
menuonly=true
value=lock

invalidmsg=const "ERROR: Status available: lock, no password, password."
choicemsg=const "Select password status for login."

name=Maximum number of days the password is valid:
lininfo=Form.addpswd:maximum
nrow=2
ncol=3
frow=2
fcol=49
rows=1
columns=8

value=`getdfl -x /etc/default/passwd`
valid=`valint -- "$F2"`

invalidmsg=const `errint`

name=Minimum number of days allowed between password changes:
lininfo=Form.addpswd:minimum
nrow=3
ncol=3
frow=3
fcol=60
rows=1
columns=8

value=`getdfl -n /etc/default/passwd`
valid=`valint -- "$F3"`

invalidmsg=const `errint`

name=Number of days for warning message:
lininfo=Form.addpswd:warn
nrow=4
ncol=3
frow=4
fcol=39
rows=1
columns=8

value=`getdfl -w /etc/default/passwd`
valid=`valint -- "$F4"`

invalidmsg=const `errint`

name=RESET
button=8
action=RESET
