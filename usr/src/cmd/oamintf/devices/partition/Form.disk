#ident	"@(#)oamintf:devices/partition/Form.disk	1.6"

################################################################################
#	Module Name: Form.disk
################################################################################
form="Partition Disks"

framemsg=`readfile $INTFBASE/form.msg`

help=OPEN TEXT $INTFBASE/Text.oamhelp $LININFO

close=`rm -f $PARTFILE $NPFILE $ERR`

done=`set -l ERR=/tmp/$VPID.err;

	set -l PARTFILE=/tmp/$VPID.pf;
	set -l NPFILE=/tmp/$VPID.npf;

	devattr $F2 cdevice | set -l DEV;
	devattr $F2 capacity | set -l DCAP;
	expr $DCAP - 1 | set -l DLAST;

	devreserv $VPID $F2 > /dev/null 2> $ERR;

	if [ "$RET" -eq 0 ];
	then
		prtvtoc -s $DEV > /dev/null 2> /dev/null;

		if [ "$RET" -eq 0 ];
		then
			set -l CMD="OPEN TEXT $OBJ_DIR/Text.part $F2";
		else
			set -l CMD="OPEN FORM $OBJ_DIR/Form.part $F2";
		fi;
	else
		set -l CMD="OPEN TEXT $OBJ_DIR/Text.parterr $F2";
	fi`$CMD

name=Device group:
lininfo=devgrp
nrow=1
ncol=3
frow=1
fcol=17
rows=1
columns=14
show=`showgrp disk;
	if [ "$RET" -eq 0 ];
	then
	  echo true;
	else
	  echo false;
	fi`


rmenu={ `getdgrp type=disk | sort` }

valid=`getdgrp type=disk $F1 | grep disk > /dev/null;
	if [ "$RET" -eq 0 ];
	then
	  echo true;
	else
	  echo false;
	fi`

invalidmsg=const "ERROR: invalid device group entered."

name=Device drive:
lininfo=devdr
devdr
nrow=2
ncol=3
frow=2
fcol=17
rows=1
columns=14

rmenu=`ckdisk;
	if [ "$RET" -eq 0 ];
	then
		set -l CMD="OPEN MENU $OBJ_DIR/../Menu.drive $F1 disk";
	else
		set -l CMD="OPEN TEXT $OBJ_DIR/../Text.diskerr";
	fi`$CMD

valid=`/usr/sadm/sysadm/bin/valdisk $F1 $F2`

invalidmsg=const "ERROR: device unknown or device contains mounted file systems"

name=RESET
button=8
action=RESET
