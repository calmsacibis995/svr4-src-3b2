#ident	"@(#)oamintf:syssetup/Form.datetime	1.3"
################################################################################
#	Module Name: Form.datetime FROM datetime:Form.dateset
#	Calling Sequence: This form is invoked through the 
#			  "main:syssetup:setup" task option 
#			  (sysssetup.menu).
#	Functional Description:	Prompt the user for setting the date, time
#                               and timezone of the system and then calling
#                               subsequent forms to take appropriate action.
#	Inputs: None.
#	Outputs: Variables to the dateset shell script for processing of date, 
#                time and timezone.
#	Functions Called: 
#			  dateset, Menu.timezones, Text.dtok, Text.dterr
#	Issues:
################################################################################

Form="Set System Date and Time Information"

`gettz`

help=OPEN TEXT $INTFBASE/Text.itemhelp $LININFO

framemsg=`readfile $INTFBASE/form.msg`

begrow=current
begcol=current

done=`message -f -w "Setting System Date and Time Information.";
      datechk $F2 $F3 $F5;
      if [ $RET -eq 0 ] ;
      then
      	dateset $F2 $F3 $F5 $F7 $F8 $F9 $F10 $F11;
      	if [ $RET -eq 0 ] ;
      	then
			set -l SETCMD="OPEN TEXT $OBJ_DIR/Text.dtok $F2 $F3 $F5 $F7 $F8 $F9 $F10";
      	else
			set -l SETCMD="OPEN TEXT $OBJ_DIR/Text.dterr $F2 $F3 $F5 $F7 $F8 $F9 $F10";
      	fi ;
      else
	message "ERROR: Invalid Date. Please check Date." ;
	set -l SETCMD="REFRESH" ;
      fi`$SETCMD

name="Date:"
lininfo="Form.datetime:date"
nrow=1
ncol=1

name=""
lininfo="Form.datetime:month"
nrow=1
ncol=19
frow=1
fcol=20
rows=1
columns=9
value=`date +%B`
choicemsg=const 'Please enter the Month (Jan - Dec) or (January - December)'
invalidmsg=const 'Error - Please enter the Month (Jan - Dec) or (January - December)'
valid=`valdate -f %b $F2 || valdate -f %B $F2`

name=""
lininfo="Form.datetime:day"
nrow=1
ncol=29
frow=1
fcol=30
rows=1
columns=2
value=`date +%d`
choicemsg=const 'Please enter the Day (01-31 or 1-31)'
invalidmsg=const 'Error - Please enter the Day (01-31 or 1-31)'
valid=`valdate -f %e $F3 || valdate -f %d $F3`

name=","
nrow=1
ncol=32

name=""
lininfo="Form.datetime:year"
nrow=1
ncol=33
frow=1
fcol=34
rows=1
columns=4
value=`date +%Y`
choicemsg=const 'Please enter the Year (greater than or equal to 1970).'
valid=`valdate -f %Y $F5`
invalidmsg=const "ERROR: Please enter the Year (greater than or equal to 1970)."

name="Time:"
lininfo="Form.datetime:time"
nrow=2 
ncol=1


name=""
lininfo="Form.datetime:hour"
nrow=2
ncol=19
frow=2
fcol=20
rows=1
columns=2
value=`date +%I`
choicemsg=const 'Please enter the Hour (00-12 or 0-12)'
invalidmsg=const 'Error - Please enter the Hour (00-12 or 0-12)'
valid=`regex -v "$F7" '^[0-9]$' || valtime -f %I $F7`

name=":"
lininfo="Form.datetime:minute"
nrow=2 
ncol=22
frow=2
fcol=23
rows=1
columns=2
value=`date +%M`
choicemsg=const 'Please enter the Minute (00-59 or 0-59)'
invalidmsg=const 'Error - Please enter the Minute (00-59 or 0-59)'
valid=`regex -v "$F8" '^[0-5][0-9]$' || valtime -f %M $F8`

name="AM/PM:"
lininfo="Form.datetime:meridian"
nrow=3
ncol=1
frow=3
fcol=20
rows=1
columns=2
value=`date +%p`
choicemsg=const 'Please enter AM/PM or am/pm'
invalidmsg=const 'Error - Please enter AM/PM or am/pm'
valid=`valtime -f %p $F9`

name="Timezone:"
lininfo="timezone"
nrow=4
ncol=1
frow=4
fcol=20
rows=1
columns=9
value=`cat /tmp/gettz`
rmenu=OPEN MENU $OBJ_DIR/Menu.timezones
valid=`regex -v "$F10" 'Greenwich' 'true' 'Atlantic' 'true' 'Eastern' 'true' 'Central' 'true' 'Mountain' 'true' 'Pacific' 'true' 'Yukon' 'true' 'Alaska' 'true' 'Bering' 'true' 'Hawaii' 'true' "$TZ" 'true'`
invalidmsg=const 'Not an available time zone. See choices.'

name="Is daylight savings time in effect?:"
lininfo="Form.datetime:daylight"
nrow=5
ncol=1
frow=5
fcol=38
rows=1
columns=3
value=`echo $TZ | fmlgrep "DT" >/dev/null; regex -v $RET '0' 'yes' '1' 'no'`
valid=`regex -v "$F11" '^yes$' 'true' '^Yes$' 'true' '^YES$' 'true' '^y$' 'true' '^Y$' 'true' '^no$' 'true' '^No$' 'true' '^NO$' 'true' '^n$' 'true' '^N$' 'true'`
choicemsg=const "Please enter yes or no - Do you want Daylight Savings Time?"
invalidmsg=const "ERROR: Please enter yes or no. Do you want Daylight Savings Time?"

name=""
nrow=7
ncol=1
frow=7
fcol=1
rows=1
columns=60
value=const "Please Note: This task should be done in single-user mode."
inactive=true

name=""
nrow=8
ncol=1
frow=8
fcol=1
rows=1
columns=50
value=const "Warning: Setting the date/time while the system"
#value=const "It's bad practice to change the date while the system is running multi-user."
inactive=true

name=""
nrow=9
ncol=1
frow=9
fcol=1
rows=1
columns=50
value="         is in multi-user state can be dangerous."
inactive=true
 
#name="Date and Time will be set to : "
#nrow=11
#ncol=1
#frow=11
#fcol=32
#rows=1
#columns=40
#value=vary "$F2 $F3, $F5 $F7:$F8 $F9 $F10"
#inactive=true

#for a form object

name=RESET
button=8
action=reset

